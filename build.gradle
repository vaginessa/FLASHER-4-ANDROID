buildscript {
    apply from: 'versions.gradle'
    addRepos(repositories)
    dependencies {
        classpath "com.android.tools.build:gradle:$versions.android_gradle_plugin"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
    }
}

allprojects {
    addRepos(repositories)
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion build_versions.target_sdk
    buildToolsVersion build_versions.build_tools

    defaultConfig {
        applicationId "com.victorlapin.flasher"
        minSdkVersion build_versions.min_sdk
        targetSdkVersion build_versions.target_sdk
        versionCode app_versions.version_code
        versionName app_versions.version_name
        archivesBaseName = "Flasher"
    }

    signingConfigs {
        debug {
            v1SigningEnabled true
            v2SigningEnabled true
        }
        release {
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    buildTypes {
        debug {
            versionNameSuffix "-debug"
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    sourceSets {
        main.res.srcDirs = ['res']
        main.java.srcDirs = ['src']
        main.manifest.srcFile 'AndroidManifest.xml'
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

apply from: 'keystore.gradle'

dependencies {
    kapt "com.arello-mobile:moxy-compiler:$versions.moxy"
    kapt "android.arch.persistence.room:compiler:$versions.room"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$versions.kotlin"
    implementation "com.android.support:design:$versions.support"
    implementation "com.android.support:cardview-v7:$versions.support"
    implementation "com.android.support:preference-v14:$versions.support"
    implementation "com.android.support.constraint:constraint-layout:$versions.constraint_layout"
    implementation "com.arello-mobile:moxy-app-compat:$versions.moxy"
    implementation "io.reactivex.rxjava2:rxjava:$versions.rxjava2"
    implementation "io.reactivex.rxjava2:rxandroid:$versions.rx_android"
    implementation "android.arch.persistence.room:runtime:$versions.room"
    implementation "android.arch.persistence.room:rxjava2:$versions.room"
    implementation "org.koin:koin-android:$versions.koin"
    implementation "ru.terrakok.cicerone:cicerone:$versions.cicerone"
    implementation "com.amulyakhare:com.amulyakhare.textdrawable:$versions.text_drawable"
    implementation "com.github.ittianyu:BottomNavigationViewEx:$versions.bottom_ex"
    implementation "com.afollestad.material-dialogs:core:$versions.dialogs"
    implementation "com.tbruyelle.rxpermissions2:rxpermissions:${versions.rx_permissions}@aar"
    implementation "com.github.topjohnwu:libsu:${versions.libsu}"

    debugImplementation "im.dino:dbinspector:${versions.db_inspector}@aar"
}
