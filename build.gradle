buildscript {
    ext {
        version_room = "2.1.0-alpha03"
        version_kotlin = "1.3.11"
        version_moxy = "1.5.5"
        version_test_runner = "1.1.0"
        version_dialogs = "2.0.0-rc4"
    }
    apply from: 'repos.gradle'
    addRepos(repositories)
    dependencies {
        classpath 'com.android.tools.build:gradle:3.3.0-rc03'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$version_kotlin"
        classpath 'com.google.android.gms:oss-licenses-plugin:0.9.4'
    }
}

allprojects {
    addRepos(repositories)
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.android.gms.oss-licenses-plugin'

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"

    defaultConfig {
        applicationId "com.victorlapin.flasher"
        minSdkVersion 24
        targetSdkVersion 28
        versionCode 44
        versionName "1.3.1-release"
        archivesBaseName = "Flasher"
        resConfigs "en", "ru"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        debug {
            v1SigningEnabled true
            v2SigningEnabled true
        }
        release {
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

            applicationVariants.all { variant ->
                variant.outputs.all {
                    if (outputFileName == (archivesBaseName + '-release.apk')) {
                        outputFileName = archivesBaseName + "-v" + defaultConfig.versionName + ".apk"
                    }
                }
            }
        }
    }

    sourceSets {
        main.res.srcDirs = ['res']
        main.java.srcDirs = ['src']
        main.manifest.srcFile 'AndroidManifest.xml'
        androidTest.java.srcDirs = ['androidTest']
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
        exclude 'META-INF/proguard/androidx-annotations.pro'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

apply from: 'keystore.gradle'

dependencies {
    kapt "com.arello-mobile:moxy-compiler:$version_moxy"
    kapt "androidx.room:room-compiler:$version_room"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$version_kotlin"
    implementation "com.google.android.material:material:1.0.0"
    implementation "androidx.preference:preference:1.0.0"
    implementation "androidx.constraintlayout:constraintlayout:2.0.0-alpha3"
    implementation "com.arello-mobile:moxy:$version_moxy"
    implementation "io.reactivex.rxjava2:rxjava:2.2.4"
    implementation "io.reactivex.rxjava2:rxandroid:2.1.0"
    implementation "androidx.room:room-runtime:$version_room"
    implementation "androidx.room:room-rxjava2:$version_room"
    implementation "org.koin:koin-android-scope:1.0.2"
    implementation "ru.terrakok.cicerone:cicerone:4.0.2"
    implementation "com.amulyakhare:com.amulyakhare.textdrawable:1.0.1"
    implementation "com.afollestad.material-dialogs:core:$version_dialogs"
    implementation "com.afollestad.material-dialogs:input:$version_dialogs"
    implementation "com.afollestad.material-dialogs:files:$version_dialogs"
    implementation ("com.github.tbruyelle:rxpermissions:0.10.2") {
        exclude group: "io.reactivex.rxjava2"
    }
    implementation "com.github.topjohnwu:libsu:2.1.2"
    implementation "com.google.code.gson:gson:2.8.5"
    implementation "com.jakewharton.timber:timber:4.7.1"
    implementation "android.arch.work:work-rxjava2:1.0.0-beta01"
    implementation "androidx.biometric:biometric:1.0.0-alpha03"
    implementation "com.google.android.gms:play-services-oss-licenses:16.0.1"

    debugImplementation "im.dino:dbinspector:3.4.1@aar"

    testImplementation "junit:junit:4.12"
    androidTestImplementation "androidx.test.ext:junit:1.1.0"
    androidTestImplementation "androidx.room:room-testing:$version_room"
    androidTestImplementation "androidx.test:runner:$version_test_runner"
    androidTestImplementation "androidx.test:rules:$version_test_runner"
}
