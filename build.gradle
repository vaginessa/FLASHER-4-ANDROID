buildscript {
    ext {
        version_room = "1.1.1"
        version_support = "27.1.1"
        version_kotlin = "1.2.50"
        version_moxy = "1.5.5"
        version_test_runner = "1.0.2"
    }
    apply from: 'repos.gradle'
    addRepos(repositories)
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.0-beta01'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$version_kotlin"
    }
}

allprojects {
    addRepos(repositories)
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 27
    buildToolsVersion "28.0.0"

    defaultConfig {
        applicationId "com.victorlapin.flasher"
        minSdkVersion 24
        targetSdkVersion 27
        versionCode 6
        versionName "1.0.0-beta6"
        archivesBaseName = "Flasher"
        resConfigs "en", "ru"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        debug {
            v1SigningEnabled true
            v2SigningEnabled true
        }
        release {
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    buildTypes {
        debug {
            versionNameSuffix "-debug"
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

            applicationVariants.all { variant ->
                variant.outputs.all {
                    if (outputFileName == (archivesBaseName + '-release.apk')) {
                        outputFileName = archivesBaseName + "-v" + defaultConfig.versionName + ".apk"
                    }
                }
            }
        }
    }

    sourceSets {
        main.res.srcDirs = ['res']
        main.java.srcDirs = ['src']
        main.manifest.srcFile 'AndroidManifest.xml'
        androidTest.java.srcDirs = ['androidTest']
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

apply from: 'keystore.gradle'

dependencies {
    kapt "com.arello-mobile:moxy-compiler:$version_moxy"
    kapt "android.arch.persistence.room:compiler:$version_room"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$version_kotlin"
    implementation "com.android.support:design:$version_support"
    implementation "com.android.support:cardview-v7:$version_support"
    implementation "com.android.support:preference-v14:$version_support"
    implementation "com.android.support.constraint:constraint-layout:1.1.2"
    implementation "com.arello-mobile:moxy-app-compat:$version_moxy"
    implementation "io.reactivex.rxjava2:rxjava:2.1.14"
    implementation "io.reactivex.rxjava2:rxandroid:2.0.2"
    implementation "android.arch.persistence.room:runtime:$version_room"
    implementation "android.arch.persistence.room:rxjava2:$version_room"
    implementation "org.koin:koin-android:0.9.3"
    implementation "ru.terrakok.cicerone:cicerone:3.0.0"
    implementation "com.amulyakhare:com.amulyakhare.textdrawable:1.0.1"
    implementation "com.github.ittianyu:BottomNavigationViewEx:1.2.4"
    implementation "com.afollestad.material-dialogs:core:0.9.6.0"
    implementation "com.tbruyelle.rxpermissions2:rxpermissions:0.9.5@aar"
    implementation "com.github.topjohnwu:libsu:1.1.1"
    implementation "com.google.code.gson:gson:2.8.5"

    debugImplementation "im.dino:dbinspector:3.4.1@aar"

    testImplementation "junit:junit:4.12"
    androidTestImplementation "android.arch.persistence.room:testing:$version_room"
    androidTestImplementation "com.android.support.test:runner:$version_test_runner"
    androidTestImplementation "com.android.support.test:rules:$version_test_runner"
}
