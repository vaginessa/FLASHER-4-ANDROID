buildscript {
    ext {
        version_room = "1.1.0"
        version_support = "27.1.1"
        version_constraint_layout = "1.1.1"
        version_rxjava2 = "2.1.14"
        version_rxandroid = "2.0.2"
        version_kotlin = "1.2.41"
        version_moxy = "1.5.5"
        version_cicerone = "3.0.0"
        version_koin = "0.9.3"
        version_text_drawable = "1.0.1"
        version_db_inspector = "3.4.1"
        version_bottom_ex = "1.2.4"
        version_dialogs = "0.9.6.0"
        version_rxpermissions = "0.9.5"
        version_libsu = "1.1.1"
        version_gson = "2.8.5"
        version_junit = "4.12"
        version_test_runner = "1.0.2"
    }
    apply from: 'repos.gradle'
    addRepos(repositories)
    dependencies {
        classpath "com.android.tools.build:gradle:3.1.3"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$version_kotlin"
    }
}

allprojects {
    addRepos(repositories)
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 27
    buildToolsVersion "28.0.0"

    defaultConfig {
        applicationId "com.victorlapin.flasher"
        minSdkVersion 24
        targetSdkVersion 27
        versionCode 6
        versionName "1.0.0-beta6"
        archivesBaseName = "Flasher"
        resConfigs "en", "ru"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        debug {
            v1SigningEnabled true
            v2SigningEnabled true
        }
        release {
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    buildTypes {
        debug {
            versionNameSuffix "-debug"
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

            applicationVariants.all { variant ->
                variant.outputs.all {
                    if (outputFileName == (archivesBaseName + '-release.apk')) {
                        outputFileName = archivesBaseName + "-v" + defaultConfig.versionName + ".apk"
                    }
                }
            }
        }
    }

    sourceSets {
        main.res.srcDirs = ['res']
        main.java.srcDirs = ['src']
        main.manifest.srcFile 'AndroidManifest.xml'
        androidTest.java.srcDirs = ['androidTest']
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

apply from: 'keystore.gradle'

dependencies {
    kapt "com.arello-mobile:moxy-compiler:$version_moxy"
    kapt "android.arch.persistence.room:compiler:$version_room"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$version_kotlin"
    implementation "com.android.support:design:$version_support"
    implementation "com.android.support:cardview-v7:$version_support"
    implementation "com.android.support:preference-v14:$version_support"
    implementation "com.android.support.constraint:constraint-layout:$version_constraint_layout"
    implementation "com.arello-mobile:moxy-app-compat:$version_moxy"
    implementation "io.reactivex.rxjava2:rxjava:$version_rxjava2"
    implementation "io.reactivex.rxjava2:rxandroid:$version_rxandroid"
    implementation "android.arch.persistence.room:runtime:$version_room"
    implementation "android.arch.persistence.room:rxjava2:$version_room"
    implementation "org.koin:koin-android:$version_koin"
    implementation "ru.terrakok.cicerone:cicerone:$version_cicerone"
    implementation "com.amulyakhare:com.amulyakhare.textdrawable:$version_text_drawable"
    implementation "com.github.ittianyu:BottomNavigationViewEx:$version_bottom_ex"
    implementation "com.afollestad.material-dialogs:core:$version_dialogs"
    implementation "com.tbruyelle.rxpermissions2:rxpermissions:${version_rxpermissions}@aar"
    implementation "com.github.topjohnwu:libsu:$version_libsu"
    implementation "com.google.code.gson:gson:$version_gson"

    debugImplementation "im.dino:dbinspector:${version_db_inspector}@aar"

    testImplementation "junit:junit:$version_junit"

    androidTestImplementation "android.arch.persistence.room:testing:$version_room"
    androidTestImplementation "com.android.support.test:runner:$version_test_runner"
    androidTestImplementation "com.android.support.test:rules:$version_test_runner"
}
